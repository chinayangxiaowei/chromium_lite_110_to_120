# Copyright 2020 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//tools/grit/preprocess_if_expr.gni")
import("//tools/polymer/html_to_wrapper.gni")
import("//tools/typescript/ts_library.gni")
import("//ui/webui/resources/tools/generate_grd.gni")

assert(is_chromeos, "Nearby Share is CrOS only")

grd_prefix = "nearby_share"
preprocessed_dir = "$target_gen_dir/preprocessed"
tsc_dir = "$target_gen_dir/tsc"

# Grdp of JSON files for OS Settings.
generate_grd("build_json_grdp") {
  grd_prefix = grd_prefix
  out_grd = "$target_gen_dir/nearby_share_json_resources.grdp"
  input_files_base_dir = rebase_path(".", "//")
  input_files = [
    "nearby_share_progress_bar_dark.json",
    "nearby_share_progress_bar_light.json",
    "nearby_share_pulse_animation_dark.json",
    "nearby_share_pulse_animation_light.json",
  ]
}

# TODO(b/207087930) Consider moving these shared files to chrome://resources.
# b/207087942 provides context why chrome://resources did not work previously.
generate_grd("build_v3_grdp") {
  grd_prefix = grd_prefix
  out_grd = "$target_gen_dir/${grd_prefix}_resources_v3.grdp"
  deps = [ ":build_ts" ]
  manifest_files = [ "$target_gen_dir/build_ts.manifest" ]
  resource_path_prefix = "shared"
}

web_component_files = [
  "nearby_contact_visibility.ts",
  "nearby_device_icon.ts",
  "nearby_device.ts",
  "nearby_onboarding_one_page.ts",
  "nearby_onboarding_page.ts",
  "nearby_page_template.ts",
  "nearby_preview.ts",
  "nearby_progress.ts",
  "nearby_visibility_page.ts",
]

# Files that are passed as input to html_to_wrapper().
html_files = []
foreach(f, web_component_files) {
  html_files += [ string_replace(f, ".ts", ".html") ]
}

# Add icons
html_files += [
  "nearby_shared_icons.html",
  "nearby_shared_share_type_icons.html",
]

# Files that are generated by html_to_wrapper().
html_wrapper_files = []
foreach(f, html_files) {
  html_wrapper_files += [ f + ".ts" ]
}

non_web_component_files = [
  "nearby_contact_manager.ts",
  "nearby_metrics_logger.ts",
  "nearby_share_settings_mixin.ts",
  "nearby_share_settings.ts",
  "types.ts",
]

preprocess_if_expr("preprocess") {
  in_folder = "."
  in_files = web_component_files + non_web_component_files + html_files
  out_folder = preprocessed_dir
}

html_to_wrapper("html_wrapper_files") {
  deps = [ ":preprocess" ]
  in_files = html_files
  in_folder = preprocessed_dir
  out_folder = preprocessed_dir
}

# TODO(b/270653607) Update this to generate TS sources instead and remove
# listing in tools/typescript/validate_tsconfig.py.
copy("copy_mojo") {
  deps = [
    "//chrome/browser/ui/webui/nearby_share:mojom_webui_js",
    "//chrome/browser/ui/webui/nearby_share:share_type_webui_js",
  ]
  sources = [
    "$root_gen_dir/mojom-webui/chrome/browser/ui/webui/nearby_share/nearby_share.mojom-webui.js",
    "$root_gen_dir/mojom-webui/chrome/browser/ui/webui/nearby_share/nearby_share_share_type.mojom-webui.js",
  ]
  outputs = [ "$preprocessed_dir/mojo/{{source_file_part}}" ]
}

ts_library("build_ts") {
  tsconfig_base = "//tools/typescript/tsconfig_base_polymer.json"
  deps = [
    "//ash/webui/common/resources:build_ts",
    "//third_party/polymer/v3_0:library",
    "//ui/webui/resources/cr_elements:build_ts",
    "//ui/webui/resources/js:build_ts",
    "//ui/webui/resources/mojo:build_ts",
  ]
  extra_deps = [
    ":copy_mojo",
    ":html_wrapper_files",
    ":preprocess",
  ]
  root_dir = preprocessed_dir
  in_files =
      web_component_files + non_web_component_files + html_wrapper_files + [
        "mojo/nearby_share.mojom-webui.js",
        "mojo/nearby_share_share_type.mojom-webui.js",
      ]
  out_dir = tsc_dir
  definitions = [ "//tools/typescript/definitions/chrome_send.d.ts" ]

  composite = true
}
